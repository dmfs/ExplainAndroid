// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.0.4"
    }
}

plugins {
    id 'org.dmfs.gitversion' version '0.5.0'
}


gitVersion {
    githubRepo "dmfs/explain-android"
    changes {
        are major when {
            commitMessage contains("(?i)#major\\b")
        }
        are major when {
            commitMessage contains("(?i)#break\\b")
        }
        are minor when {
            commitMessage referencesGithubIssue { issue -> issue.labels?.every { it.name != "bug" } }
        }
        are patch when {
            commitMessage referencesGithubIssue { issue -> issue.labels?.any { it.name == "bug" } }
        }
        are minor when {
            commitMessage contains("(?i)\\b(implement(s|ed)?|close[sd]?) #\\d+\\b")
        }
        are patch when {
            commitMessage contains("(?i)\\b(fix(e[sd])?|resolve[sd]?) #\\d+\\b")
        }
        otherwise patch
    }
    releaseBranchPattern ~/main$/
}

allprojects {
    group 'org.dmfs'
}


task clean(type: Delete) {
    delete rootProject.buildDir
}